import net.neoforged.gradle.common.tasks.JarJar

plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.97'
    id 'io.github.0ffz.github-packages' version '[1,2)'
    id 'com.matthewprenger.cursegradle' version '[1.4,1.5)'
    id 'com.modrinth.minotaur' version '[2,3)'
}

version = "${project.mc_version}-${project.mod_version}-neoforge"
group = 'com.aetherteam.aetherii'

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    configureEach {
        workingDirectory project.file('run')
        systemProperty "${mod_id}.iside", 'true'
        systemProperty  'forge.logging.console.level', 'info'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'neoforge.enabledGameTestNamespaces', mod_id
    }

    data {
        programArguments.addAll '--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath(), '--existing', file('src/generated/resources/').getAbsolutePath(), '--existing-mod', 'nitrogen_internals', '--existing-mod', 'cumulus_menus'
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    implementation "net.neoforged:neoforge:${project.neoforge_version}"

    implementation "com.aetherteam.nitrogen:nitrogen_internals:${project.nitrogen_version}"
    implementation "com.aetherteam.cumulus:cumulus_menus:${project.cumulus_version}"

    implementation "top.theillusivec4.curios:curios-neoforge:${project.curios_version}+${project.mc_version}"
//    implementation "software.bernie.geckolib:geckolib-neoforge-${project.mc_version}:${project.geckolib_version}"
    implementation "net.tslat.smartbrainlib:SmartBrainLib-neoforge-${project.mc_version}:${project.sbl_version}"

    compileOnly "mezz.jei:jei-${project.mc_version}-common-api:${project.jei_version}"
    compileOnly "mezz.jei:jei-${project.mc_version}-neoforge-api:${project.jei_version}"
//    runtimeOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}"))

    jarJar("com.aetherteam.nitrogen:nitrogen_internals:${project.nitrogen_version}") {
        jarJar.ranged(it, "[${project.nitrogen_version},)")
        jarJar.pin(it, "${project.nitrogen_version}")
    }

    jarJar("com.aetherteam.cumulus:cumulus_menus:${project.cumulus_version}") {
        jarJar.ranged(it, "[${project.cumulus_version},)")
        jarJar.pin(it, "${project.cumulus_version}")
    }

    jarJar("top.theillusivec4.curios:curios-neoforge:${project.curios_version}+${project.mc_version}") {
        jarJar.ranged(it, "[${project.curios_version},)")
        jarJar.pin(it, "${project.curios_version}+${project.mc_version}")
    }

//    jarJar("software.bernie.geckolib:geckolib-neoforge-${project.mc_version}:${project.geckolib_version}") {
//        jarJar.ranged(it, "[${project.geckolib_version},)")
//        jarJar.pin(it, "${project.geckolib_version}")
//    }

    jarJar("net.tslat.smartbrainlib:SmartBrainLib-neoforge-${project.mc_version}:${project.sbl_version}") {
        jarJar.ranged(it, "[${project.sbl_version},)")
        jarJar.pin(it, "${project.sbl_version}")
    }

    //compileOnly fileTree(dir: 'libs', include: '*.jar')
}

repositories {
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.blamejared.com" }
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url = "https://www.cursemaven.com" }
    maven { url = "https://dvs1.progwml6.com/files/maven/" }
    maven { url = "https://gitlab.com/api/v4/projects/21830712/packages/maven" }
//    maven { url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url = 'https://dl.cloudsmith.io/public/tslat/sbl/maven/' }
    maven githubPackage.invoke("The-Aether-Team/Nitrogen")
    maven githubPackage.invoke("The-Aether-Team/Cumulus")
    mavenLocal()
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : project.mod_name,
                "Specification-Vendor"    : "The Aether Team",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.mod_name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "The Aether Team",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    archiveClassifier = 'no-embeds'
}

tasks.named('jarJar', JarJar).configure {
    archiveClassifier = ''
}

compileJava {
    options.incremental = false
}

tasks.register('sourceJar', Jar) {
    dependsOn 'classes'
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            artifact project.tasks.jarJar
            artifact project.sourceJar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/The-Aether-Team/The-Aether-II"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

curseforge {
    project {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        id = "${project.curseforge_id}"
        releaseType = "${project.version_type}"
        changelog = file("$rootDir/docs/CHANGELOG.md")
        changelogType = "markdown"
        addGameVersion("${project.mc_version}")
        addGameVersion("NeoForge")
        mainArtifact(tasks.jarJar) {
            displayName = "${project.mod_name} - ${version}"
            relations {
                embeddedLibrary "curios"
                embeddedLibrary "cumulus"
//                embeddedLibrary "geckolib"
                embeddedLibrary "smartbrainlib"
            }
        }
    }
    options {
        debug = true
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${project.modrinth_id}"
    versionNumber = "${version}"
    versionType = "${project.version_type}"
    versionName = "${project.mod_name} - ${version}"
    changelog = new File("$rootDir/docs/CHANGELOG.md").text
    uploadFile = tasks.jarJar
    gameVersions = ["${project.mc_version}"]
    loaders = ["neoforge"]
    dependencies {
        embedded.version "curios", "${project.curios_version}"
        embedded.version "cumulus", "${project.cumulus_version}"
//        embedded.version "geckolib", "${project.geckolib_version}"
        embedded.version "smartbrainlib", "${project.sbl_version}"
    }
    debugMode = true
}

build {
    dependsOn 'jarJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}