buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

ext.config = loadConfig(file('build.properties'))

version = config.mod_version
group = 'com.gildedgames.aether'
archivesBaseName = "AetherII"

dependencies {
    compile project(":Gilded-Games-Util")
}

minecraft {
    version = config.forge_version
    mappings = config.forge_mappings

    runDir = "run"
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea {
	module {
		inheritOutputDirs = true
	}
}

def loadConfig(File config) {
	config.withReader {
		def properties = new Properties()
		properties.load(it)

		return (new ConfigSlurper().parse(properties))
	}
}
